//---------------------------------------------------------------------------//
// Preparing WAR                                                             //
//---------------------------------------------------------------------------//
task copyFrontendConfig(type: Copy) {
  from "${projectDir}/schemas/restapi/config/config.json"
  into "${projectDir}/src/main/webapp/frontend/"
}
war.dependsOn copyFrontendConfig

task copyFrontendConfigIntegrationTest(type: Copy) {
  from "${projectDir}/schemas/restapi/config/config.json"
  into "${projectDir}/src/integrationTest/resources"
}

task copyFrontendConfigEndToEndTest(type: Copy) {
  from "${projectDir}/schemas/restapi/config/config.json"
  into "${projectDir}/src/endToEndTest/resources"
}

task removeFrontendConfig(type: Delete) {
  delete "${projectDir}/src/main/webapp/frontend/"
  delete "${projectDir}/src/integrationTest/resources/config.json"
  delete "${projectDir}/src/endToEndTest/resources/config.json"
}

task copyWarForIntegration(type: Copy, dependsOn: war) {
  from "${war.archivePath}"
  into "${projectDir}/src/integrationTest/image"
  doLast{
    file("${projectDir}/src/integrationTest/image/${war.archiveName}").renameTo("${projectDir}/src/integrationTest/image/radar.war")
  }
}

task removeWar(type: Delete) {
  delete "${projectDir}/src/integrationTest/image/radar.war"
}

//---------------------------------------------------------------------------//
// Docker Utility                                                            //
//---------------------------------------------------------------------------//
ext.dockerCompose = hasProperty('dockerComposePath') ? property('dockerComposePath') : 'docker-compose'
ext.sudoLinux = System.properties['os.name'].toLowerCase().contains('linux') ? ['sudo'] : []

//---------------------------------------------------------------------------//
// Simple Stack [ for integration test ]                                     //
//---------------------------------------------------------------------------//
ext.simpleStackPath = 'src/integrationTest'
ext.simpleStackLock = "${simpleStackPath}/.RUNNING_INSTANCE_LOCK"

task buildDocker(type: Exec, dependsOn: copyWarForIntegration) {
  workingDir 'src/integrationTest/image'
  commandLine sudoLinux + ['docker', 'image', 'build', '-t', 'radarcns/radar-restapi:dev', '.']
}

task installSimpleStack(type: Exec, dependsOn: buildDocker) {
  doFirst{
    def lockFile = new File(simpleStackLock)
    if (lockFile.exists()) {
      throw new GradleException("A previous instance of the simple stack is still running. Please stop it first and then try again.")
    } else {
      lockFile.createNewFile()
    }

    def radarVolumePath = "${simpleStackPath}/volumes/radar"
    file(radarVolumePath).mkdirs()

    def configJson = file("${simpleStackPath}/resources/config.json")
    file("${radarVolumePath}/config.json") << configJson.text

    def catalogYml = file("${simpleStackPath}/resources/device-catalog.yml")
    file("${radarVolumePath}/device-catalog.yml") << catalogYml.text

    def radarYml = file("${simpleStackPath}/resources/radar.yml")
    file("${radarVolumePath}/radar.yml") << radarYml.text.replace("localhost: 27017", "hotstorage: 27017")
    
    def mpYml = file("${simpleStackPath}/resources/mp_info.yml")
    file("${radarVolumePath}/mp_info.yml") << mpYml.text
  }

  workingDir simpleStackPath
  standardInput = System.in
  commandLine sudoLinux + [dockerCompose, 'up', '-d', '--build']

  doLast {
    // wait until the hotstorage is ready
    sleep(10_000)
  }
}

task stopSimpleStack(type: Exec) {
  onlyIf{
    new File(simpleStackLock).exists()
  }
  standardInput = System.in
  workingDir simpleStackPath
  commandLine sudoLinux + [dockerCompose, 'down', '-v']
  doLast{
    new File(simpleStackLock).delete()
  }
}

task cleanSimpleStack(type: Delete, dependsOn: stopSimpleStack) {
  delete "${simpleStackPath}/volumes"
  delete simpleStackLock
}

stopSimpleStack.finalizedBy cleanSimpleStack
stopSimpleStack.finalizedBy removeWar
stopSimpleStack.finalizedBy removeFrontendConfig
//---------------------------------------------------------------------------//
// End to End Test                                                           //
//---------------------------------------------------------------------------//
ext.radarStackPath = 'src/endToEndTest/dockerRadar/dcompose-stack/radar-cp-hadoop-stack'
ext.radarStackLock = "${radarStackPath}/.RUNNING_INSTANCE_LOCK"
ext.radarDockerCompose = 'docker-compose.yml'
ext.radarDockerComposeOriginal = ".${radarDockerCompose}.orig"

// Sets configuration files for running the entire platform
task setRadarEnvironmentForDocker(dependsOn: buildDocker) {
  doLast {
    def envTemplatePath = "${radarStackPath}/etc/env.template"
    def smtpTemplatePath = "${radarStackPath}/etc/smtp.env.template"
    def streamTemplatePath = "${radarStackPath}/etc/radar.yml.template"

    def volumes = file("${radarStackPath}/volumes")
    volumes.mkdir()

    def fileEnv = file(envTemplatePath + 'tmp')
    fileEnv << file(envTemplatePath).text
    fileEnv.text = fileEnv.text.replace('/usr/local/var/lib/docker', volumes.getAbsolutePath())
    fileEnv.text = fileEnv.text.replace('HOTSTORAGE_USERNAME=<mongodb-user>', 'HOTSTORAGE_USERNAME=restapi')
    fileEnv.text = fileEnv.text.replace('HOTSTORAGE_PASSWORD=XXXXXXXX', 'HOTSTORAGE_PASSWORD=radarcns')
    fileEnv.text = fileEnv.text.replace('HOTSTORAGE_NAME=<mongodb-database>', 'HOTSTORAGE_NAME=hotstorage')
    fileEnv.renameTo("${radarStackPath}/.env")

    def fileSmtp = file(smtpTemplatePath + 'tmp')
    fileSmtp << file(smtpTemplatePath).text
    fileSmtp.renameTo("${radarStackPath}/etc/smtp.env")

    def fileStream = file(streamTemplatePath + 'tmp')
    fileStream << file(streamTemplatePath).text
    fileStream.renameTo("${radarStackPath}/etc/radar.yml")

    def installScript = file("${radarStackPath}/install-radar-stack.sh")
    installScript.text = installScript.text.replace(' docker-compose ', " ${dockerCompose} ")

    def dockerComposeYml = file("${radarStackPath}/${radarDockerCompose}")
    file("${radarStackPath}/${radarDockerComposeOriginal}") << dockerComposeYml.text
    def dockerComposeTmp = file("${radarStackPath}/.${radarDockerCompose}.tmp")
    dockerComposeTmp << dockerComposeYml.text.replaceAll('radarcns/radar-restapi:.*', 'radarcns/radar-restapi:dev')
    dockerComposeTmp.renameTo("${radarStackPath}/${radarDockerCompose}")
  }
}

ext.containers = ['zookeeper-1', 'kafka-1', 'kafka-2', 'kafka-3', 'schema-registry-1',
                  'rest-proxy-1', 'kafka-init', 'hotstorage', 'rest-api',
                  'radar-mongodb-connector', 'radar-backend-stream', 'webserver']
task reducedStack(dependsOn: setRadarEnvironmentForDocker) {
  doLast {
    def dockerComposeYml = file("${radarStackPath}/${radarDockerCompose}")
    def findContainer = false
    def findDependences = false
    def active = false

    LinkedList rowList = new LinkedList()

    dockerComposeYml.eachLine { line ->
      def row = line.trim()
      def comment = false
      def index = rowList.size() - 1

      if (active) {
        if (row.contains('image:')) {
          findContainer = false
          def containerName = rowList.last().replace('#', '')
          if (containerName.contains('build:')) {
            index--
            containerName = rowList.get(index).replace('#', '')
          }
          containerName = containerName.trim()
          containers.each {container ->
            if ( containerName == "$container:") {
              findContainer = true
            }
          }

          if (findContainer) {
            for (def i = rowList.size() - 1 ; i >= index; i--) {
              rowList.add(i, rowList.remove(i).replace('#', ''))
            }
          } else {
            for (def i = rowList.size() - 1 ; i >= index; i--) {
              rowList.add(i, "#${rowList.remove(i)}")
            }
          }
        }

        comment = !findContainer

        if (findContainer && row == 'depends_on:') {
          findDependences = true
        } else if (findContainer && findDependences && !row.contains('-')) {
          findDependences = false
        }

        if (findContainer && findDependences && row != 'depends_on:') {
          comment = true
          containers.each {container ->
            if (row.contains(container)) {
              comment = false
            }
          }
        }
      }

      comment ? rowList.addLast("#$line") : rowList.addLast(line)

      if (row == 'services:') {
        active = true
      }
    }

    PrintWriter writer = new PrintWriter(new File("${radarStackPath}/${radarDockerCompose}"))
    rowList.each { row -> writer.println(row) }
    writer.close()
  }
}

task optimiseNginx() {
  doLast {
    LinkedList rowList = new LinkedList()
    def nginxConfig = new File("${radarStackPath}/etc/nginx.conf.template")

    def comment = false
    def findBlock = false
    nginxConfig.eachLine { line ->
      if (line.contains('{')) {
        findBlock = true
      } else if (line.contains('}')) {
        findBlock = false
      }

      if (findBlock && line.contains('proxy_pass')) {
        comment = true
        containers.each {container ->
          if (line.contains(container)) {
            comment = false
            rowList.addLast(rowList.removeLast().replace('#', ''))
          }
        }
        if (comment) {
          rowList.addLast("# ${rowList.removeLast()}")
        }
      }

      comment ? rowList.addLast("# $line") : rowList.addLast(line)
    }

    PrintWriter writer = new PrintWriter(new File("${radarStackPath}/etc/nginx.conf"))
    rowList.each { row -> writer.println(row) }
    writer.close()
  }
}
reducedStack.finalizedBy optimiseNginx

task validateCompose(type: Exec, dependsOn: reducedStack) {
  workingDir radarStackPath
  standardInput = System.in
  commandLine sudoLinux + [dockerCompose, 'config', '-q']
}

task installRadarStack(type: Exec, dependsOn: validateCompose) {
  doFirst{
    def lockFile = new File(radarStackLock)
    if (lockFile.exists()) {
      throw new GradleException("A previous instance of RADAR Platform is still running. Please stop it first and then try again.")
    } else {
      lockFile.createNewFile()
    }
  }

  workingDir radarStackPath
  standardInput = System.in
  commandLine sudoLinux + ['./install-radar-stack.sh']
}

task listDockerProcesses(type: Exec) {
  workingDir radarStackPath
  standardInput = System.in
  commandLine sudoLinux + [dockerCompose, 'ps']
}
listDockerProcesses.mustRunAfter 'installRadarStack'

ext.stoppingStack = false
task stopRadarStack(type: Exec) {
  onlyIf{
    new File(radarStackLock).exists() && !stoppingStack
  }
  doFirst{
    stoppingStack = true
  }

  standardInput = System.in
  workingDir radarStackPath
  commandLine sudoLinux + [dockerCompose, 'down', '-v']

  doLast{
    stoppingStack = false
  }
}

task cleanRadarStack(type: Exec, dependsOn: stopRadarStack) {
  onlyIf{
    !stoppingStack
  }
  commandLine sudoLinux + ['rm', '-rf', "${radarStackPath}/volumes", radarStackLock]
  doLast{
    file("${radarStackPath}/${radarDockerComposeOriginal}").renameTo("${radarStackPath}/${radarDockerCompose}")
  }
}

stopRadarStack.finalizedBy cleanRadarStack
stopRadarStack.finalizedBy removeWar
stopRadarStack.finalizedBy removeFrontendConfig
